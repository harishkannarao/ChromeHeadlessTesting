buildscript {
    dependencies { classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}") }
}

apply plugin: 'org.springframework.boot'

/*
'selenium.version' properties is used by Spring Boot Dependency Management plugin.
Unless we set the version to be used by the project, Spring Boot forces it's own version.
Execute 'printSpringBootProperties' to get the list of properties used by Spring Boot.
Execute 'printSpringBootManagedVersions' to get the list of libraries managed by Spring Boot.
 */
ext['selenium.version']="$seleniumVersion"

task('printSpringBootProperties') {
    doFirst {
        dependencyManagement.importedProperties.each { k, v -> println "${k}:${v}" }
    }
}

task('printSpringBootManagedVersions') {
    doFirst {
        dependencyManagement.managedVersions.each { k, v -> println "${k}:${v}" }
    }
}

bootJar {
    baseName = 'java-script-jasmine-unit-tests'
    classifier = "$appVersion-exec"
}

bootRun {
    sourceResources sourceSets.main
}

jar {
    enabled = true
    baseName = 'java-script-jasmine-unit-tests'
    version =  "$appVersion"
}

dependencies {
    compile project(':StaticContent')

    testCompile project(':TestCommon').sourceSets.test.output
}